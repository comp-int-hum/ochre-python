<?xml version="1.0" encoding="UTF-8"?>
<xsl:transform
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
    xmlns:ochre="urn:ochre:"
    version="1.0"
    >
  
  <xsl:output method="xml" indent="yes"/>

  <xsl:param name="ochre_namespace"/>
  <xsl:variable name="xsd">http://www.w3.org/2001/XMLSchema#</xsl:variable>
  
  <!-- do nothing with random text -->  
  <xsl:template match="text()|@*"/>

  <!-- top level of document -->
  <xsl:template match="/">
    <rdf:RDF xml:base="{$ochre_namespace}">
      <xsl:apply-templates select="document/row"/>
    </rdf:RDF>
  </xsl:template>

  <!-- inscription -->
  <xsl:template match="/document/row">
    <xsl:variable name="id" select="concat('P', cell[@id='id']/@value)"/>
    <rdf:Description>
      <isA rdf:resource="Inscription" />
      <hasLabel rdf:datatype="{$xsd}string">
	<xsl:value-of select="$id" />
      </hasLabel>
      <hasTransliteration rdf:datatype="{$xsd}string">
	<xsl:value-of select="cell[@id='content']/@value" />
      </hasTransliteration>
      <xsl:apply-templates select="cell" />

      <hasImage rdf:parseType="Resource">
	<isA rdf:resource="LineDrawing" />
	<hasFile rdf:parseType="Resource">
	  <isA rdf:resource="ImageFile" />
	  <hasLabel rdf:datatype="{$xsd}string">
	    <xsl:value-of select="concat($id, '_line.jpg')" />
	  </hasLabel>
	</hasFile>
      </hasImage>

      <hasImage rdf:parseType="Resource">
	<isA rdf:resource="Photo" />
	<hasFile rdf:parseType="Resource">
	  <isA rdf:resource="ImageFile" />
	  <hasLabel rdf:datatype="{$xsd}string">
	    <xsl:value-of select="concat($id, '_photo.jpg')" />
	  </hasLabel>
	</hasFile>
      </hasImage>
    </rdf:Description>
  </xsl:template>

  <!-- metadata item -->
  <xsl:template match="/document/row/cell[@id='material']">
    <madeFrom rdf:datatype="{$xsd}string">
      <xsl:value-of select="@value"/>
    </madeFrom>
  </xsl:template>
  
  <xsl:template match="/document/row/cell[@id='language']">
    <inLanguage rdf:datatype="{$xsd}string">
      <xsl:value-of select="@value"/>
    </inLanguage>
  </xsl:template>

  <xsl:template match="/document/row/cell[@id='genre']">
    <hasGenre rdf:datatype="{$xsd}string">
      <xsl:value-of select="@value"/>
    </hasGenre>
  </xsl:template>

</xsl:transform>
