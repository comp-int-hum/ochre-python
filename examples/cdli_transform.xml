<?xml version="1.0" encoding="UTF-8"?>
<xsl:transform
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
    xmlns:ochre="urn:ochre:"
    version="1.0"
    >
  
  <xsl:output method="xml" indent="yes"/>

  <xsl:param name="ochre_namespace">urn:ochre:</xsl:param>
  <xsl:variable name="xsd">http://www.w3.org/2001/XMLSchema#</xsl:variable>
  
  <!-- do nothing with random text -->  
  <xsl:template match="text()|@*"/>

  <!-- top level of document -->
  <xsl:template match="/">
    <rdf:RDF xml:base="{$ochre_namespace}">
      <xsl:apply-templates select="document/row"/>
    </rdf:RDF>
  </xsl:template>

  <!-- inscription -->
  <xsl:template match="/document/row">
    <xsl:variable name="id" select="concat('P', cell[@id='id']/@value)"/>
    <rdf:Description>
      <ochre:instanceOf rdf:resource="Inscription" />
      <ochre:hasLabel rdf:datatype="{$xsd}string">
	<xsl:value-of select="$id" />
      </ochre:hasLabel>
      <ochre:hasTransliteration rdf:datatype="{$xsd}string">
	<xsl:value-of select="cell[@id='content']/@value" />
      </ochre:hasTransliteration>
      <xsl:apply-templates select="cell" />

      <ochre:hasImage rdf:parseType="Resource">
	<ochre:instanceOf rdf:resource="LineDrawing" />
	<ochre:hasFile rdf:parseType="Resource">
	  <ochre:instanceOf rdf:resource="ImageFile" />
	  <ochre:hasLabel rdf:datatype="{$xsd}string">
	    <xsl:value-of select="concat($id, '_line.jpg')" />
	  </ochre:hasLabel>
	</ochre:hasFile>
      </ochre:hasImage>

      <ochre:hasImage rdf:parseType="Resource">
	<ochre:instanceOf rdf:resource="Photo" />
	<ochre:hasFile rdf:parseType="Resource">
	  <ochre:instanceOf rdf:resource="ImageFile" />
	  <ochre:hasLabel rdf:datatype="{$xsd}string">
	    <xsl:value-of select="concat($id, '_photo.jpg')" />
	  </ochre:hasLabel>
	</ochre:hasFile>
      </ochre:hasImage>
    </rdf:Description>
  </xsl:template>

  <!-- metadata item -->
  <xsl:template match="/document/row/cell[@id='material']">
    <ochre:madeFrom rdf:datatype="{$xsd}string">
      <xsl:value-of select="@value"/>
    </ochre:madeFrom>
  </xsl:template>
  
  <xsl:template match="/document/row/cell[@id='language']">
    <ochre:inLanguage rdf:datatype="{$xsd}string">
      <xsl:value-of select="@value"/>
    </ochre:inLanguage>
  </xsl:template>

  <xsl:template match="/document/row/cell[@id='genre']">
    <ochre:hasGenre rdf:datatype="{$xsd}string">
      <xsl:value-of select="@value"/>
    </ochre:hasGenre>
  </xsl:template>

</xsl:transform>
